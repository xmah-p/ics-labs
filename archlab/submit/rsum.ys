# 杨艺欣    2200017768


# Start program
    .pos 0
    irmovq stack, %rsp
    call main
    halt

# Sample linked list
.align 8
ele1:
    .quad 0x00a
    .quad ele2
ele2:
    .quad 0x0b0
    .quad ele3
ele3:
    .quad 0xc00
    .quad 0

# typedef struct ELE {
#     long val;
#     struct ELE* next;
# }* list_ptr;

# long rsum_list(list_ptr ls) {
#     if (!ls)
#         return 0;
#     else {
#         long val = ls->val;
#         long rest = rsum_list(ls->next);
#         return val + rest;
#     }
# }

rsum_list: 
    xorq %rax, %rax
    andq %rdi, %rdi
    je done
    mrmovq (%rdi), %rdx
    pushq  %rdx          # long val = ls->val;
    mrmovq 8(%rdi), %rdi
    call rsum_list       # long rest = rsum_list(ls->next)
    popq  %rdx
    addq %rax, %rdx
    rrmovq %rdx, %rax    # return val + rest
done:
    ret

main:	
    irmovq ele1, %rdi
	call rsum_list    # rsum_list(ele1)
	ret

    .pos 0x200
stack:
